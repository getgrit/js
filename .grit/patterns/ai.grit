language js

// Given a question and a list of choices,
function ai_ask($question, $choices) {
  $choice_text = join($choices, `, `),
  $messages = [
    {
      role: "system",
      content: `Answer all questions with a single word. Choices: $choice_text`
    },
    {
      role: "user",
      content: $question
    }
  ],
  $answer = llm_choice($messages, $choices),
  return $answer
}

// Match the given condition, optionally with examples and counter-examples.
pattern ai_is($condition, $examples, $counter_examples) {
  $item_to_check where {
    $messages = [
      {
        role: "system",
        content: `Classify whether the input code should match this condition: $condition. Answer with yes or no.`
      }
    ],
    $examples <: some bubble($messages) $example where {
      $messages += {
        role: "user",
        content: $example,
      },
      $messages += {
        role: "assistant",
        content: `yes`
      }
    },
    $counter_examples <: some bubble($messages) $counter_example where {
      $messages += {
        role: "user",
        content: $counter_example,
      },
      $messages += {
        role: "assistant",
        content: `no`
      }
    },
    $messages += {
      role: "user",
      content: $item_to_check
    },
    $answer = llm_choice($messages, choices=["yes", "no"]),
    $answer <: "yes"
  }
}
